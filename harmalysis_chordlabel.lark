// The harmalysis language for harmonic analysis and roman numerals
// The chordlabels grammar for Chord labels
//
// Copyright (c) 2019, Nestor Napoles Lopez
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//
// 1. Redistributions of source code must retain the above copyright notice, this
//    list of conditions and the following disclaimer.
//
// 2. Redistributions in binary form must reproduce the above copyright notice,
//    this list of conditions and the following disclaimer in the documentation
//    and/or other materials provided with the distribution.
//
// 3. Neither the name of the copyright holder nor the names of its
//    contributors may be used to endorse or promote products derived from
//    this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
// AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
// IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
// FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
// DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
// SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
// CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
// OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

/////////////////////////////////
// Flats, sharps, and alterations
/////////////////////////////////
FLAT :         "-"  | "b"
DOUBLE_FLAT :  "--" | "bb"
SHARP :        "#"
DOUBLE_SHARP : "##" | "x"
_alteration : FLAT
            | DOUBLE_FLAT
            | SHARP
            | DOUBLE_SHARP

///////////////////////////////////////////
// Lower-case, upper-case letters, and keys
///////////////////////////////////////////
NOTE_LETTER_LOWERCASE : "a".."g"
NOTE_LETTER_UPPERCASE : "A".."G"
LETTER : NOTE_LETTER_LOWERCASE | NOTE_LETTER_UPPERCASE

key : NOTE_LETTER_UPPERCASE [_alteration] -> major_key
    | NOTE_LETTER_LOWERCASE [_alteration] -> minor_key

root : LETTER             -> root
     | LETTER _alteration -> root_with_alteration

//////////////////
// Added intervals
//////////////////
_INTERVAL_UNISON : "1"
_INTERVAL_SECOND : "2"
_INTERVAL_THIRD : "3"
_INTERVAL_FOURTH : "4"
_INTERVAL_FIFTH : "5"
_INTERVAL_SIXTH : "6"
_INTERVAL_SEVENTH : "7"
_INTERVAL_OCTAVE : "8"
_INTERVAL_NINTH : "9"
_INTERVAL_TENTH : "10"
_INTERVAL_ELEVENTH : "11"
_INTERVAL_TWELFTH : "12"
_INTERVAL_THIRTEENTH : "13"
_INTERVAL_FOURTEENTH : "14"
_INTERVAL_FIFTEENTH : "15"

_MAJOR_INTERVAL : "M"
_MINOR_INTERVAL : "m"
_PERFECT_INTERVAL : "P"
_AUGMENTED_INTERVAL : "A"
_DIMINISHED_INTERVAL : "D"
_DOUBLE_AUGMENTED_INTERVAL : "AA"
_DOUBLE_DIMINISHED_INTERVAL : "DD"


_major_third : _MAJOR_INTERVAL _INTERVAL_THIRD
_minor_third : _MINOR_INTERVAL _INTERVAL_THIRD
_perfect_fifth : _PERFECT_INTERVAL _INTERVAL_FIFTH
_diminished_fifth : _DIMINISHED_INTERVAL _INTERVAL_FIFTH
_augmented_fifth : _AUGMENTED_INTERVAL _INTERVAL_FIFTH
_minor_seventh : _MINOR_INTERVAL _INTERVAL_SEVENTH
_major_seventh : _MAJOR_INTERVAL _INTERVAL_SEVENTH
_diminished_seventh : _DIMINISHED_INTERVAL _INTERVAL_SEVENTH

_major_triad : _major_third _perfect_fifth
_minor_triad : _minor_third _perfect_fifth
_augmented_triad : _major_third _augmented_fifth
_diminished_triad : _minor_third _diminished_fifth

_major_seventhchord : _major_triad _major_seventh
_dominant_seventhchord : _major_triad _minor_seventh

_augmented_major_seventhchord : _augmented_triad _major_seventh

_minor_seventhchord : _minor_triad _minor_seventh
_minor_major_seventhchord : _minor_triad _major_seventh

_half_diminished_seventhchord : _diminished_triad _minor_seventh
_fully_diminished_seventhchord : _diminished_triad _diminished_seventh

//////////////
// Chord label
//////////////
chord : _major_triad                   -> major_triad_chord
      | _minor_triad                   -> minor_triad_chord
      | _augmented_triad               -> augmented_triad_chord
      | _diminished_triad              -> diminished_triad_chord
      | _major_seventhchord            -> major_seventh_chord
      | _dominant_seventhchord         -> dominant_seventh_chord
      | _augmented_major_seventhchord  -> augmented_major_seventh_chord
      | _minor_seventhchord            -> minor_seventh_chord
      | _minor_major_seventhchord      -> minor_major_seventh_chord
      | _half_diminished_seventhchord  -> half_diminished_seventh_chord
      | _fully_diminished_seventhchord -> fully_diminished_seventh_chord

chordlabel : root chord

?start : chordlabel