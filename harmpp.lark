// The harm++ roman numeral analysis language
//
// Copyright (c) 2019, Nestor Napoles Lopez
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//
// 1. Redistributions of source code must retain the above copyright notice, this
//    list of conditions and the following disclaimer.
//
// 2. Redistributions in binary form must reproduce the above copyright notice,
//    this list of conditions and the following disclaimer in the documentation
//    and/or other materials provided with the distribution.
//
// 3. Neither the name of the copyright holder nor the names of its
//    contributors may be used to endorse or promote products derived from
//    this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
// AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
// IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
// FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
// DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
// SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
// CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
// OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.


/////////////////////////////////
// Flats, sharps, and alterations
/////////////////////////////////
FLAT : "-" | "b"
DOUBLE_FLAT : "--" | "bb"
SHARP : "#"
DOUBLE_SHARP : "##" | "x"
alteration : FLAT
           | DOUBLE_FLAT
           | SHARP
           | DOUBLE_SHARP

///////////////////////////////////////////
// Lower-case, upper-case letters, and keys
///////////////////////////////////////////
NOTE_LETTER_LOWERCASE : "a".."g"
NOTE_LETTER_UPPERCASE : "A".."G"
letter : NOTE_LETTER_LOWERCASE | NOTE_LETTER_UPPERCASE
key: letter [alteration] ":"

////////////////
// Scale degrees
////////////////
TONIC_UPPERCASE : "I"
SUPERTONIC_UPPERCASE : "II"
MEDIANT_UPPERCASE : "III"
SUBDOMINANT_UPPERCASE : "IV"
DOMINANT_UPPERCASE : "V"
SUBMEDIANT_UPPERCASE : "VI"
SEVENTH_DEGREE_UPPERCASE : "VII"

TONIC_LOWERCASE : "i"
SUPERTONIC_LOWERCASE : "ii"
MEDIANT_LOWERCASE : "iii"
SUBDOMINANT_LOWERCASE : "iv"
DOMINANT_LOWERCASE : "v"
SUBMEDIANT_LOWERCASE : "vi"
SEVENTH_DEGREE_LOWERCASE : "vii"

scale_degree_major : TONIC_UPPERCASE
                   | SUPERTONIC_UPPERCASE
                   | MEDIANT_UPPERCASE
                   | SUBDOMINANT_UPPERCASE
                   | DOMINANT_UPPERCASE
                   | SUBMEDIANT_UPPERCASE
                   | SEVENTH_DEGREE_UPPERCASE

scale_degree_minor : TONIC_LOWERCASE
                   | SUPERTONIC_LOWERCASE
                   | MEDIANT_LOWERCASE
                   | SUBDOMINANT_LOWERCASE
                   | DOMINANT_LOWERCASE
                   | SUBMEDIANT_LOWERCASE
                   | SEVENTH_DEGREE_LOWERCASE

scale_degree : scale_degree_major | scale_degree_minor

/////////////
// Inversions
/////////////
NO_INVERSION : "a"
FIRST_INVERSION : "b"
SECOND_INVERSION: "c"
THIRD_INVERSION: "d"
FOURTH_INVERSION: "e"
FIFTH_INVERSION: "f"
SIXTH_INVERSION: "g"
SEVENTH_INVERSION: "h"
EIGHTH_INVERSION: "i"
NINTH_INVERSION: "j"
TENTH_INVERSION: "k"
ELEVENTH_INVERSION: "l"

triad_inversion : NO_INVERSION
                | FIRST_INVERSION
                | SECOND_INVERSION

chord_inversion : triad_inversion | THIRD_INVERSION

extended_chord_inversion : chord_inversion
                         | FOURTH_INVERSION
                         | FIFTH_INVERSION
                         | SIXTH_INVERSION
                         | SEVENTH_INVERSION
                         | EIGHTH_INVERSION
                         | NINTH_INVERSION
                         | TENTH_INVERSION
                         | ELEVENTH_INVERSION

/////////////////////
// Numeric inversions
/////////////////////
NUMERIC_INVERSION_TRIAD_FIRST_INVERSION : "6"
NUMERIC_INVERSION_TRIAD_SECOND_INVERSION : "64"

numeric_inversion_triad : NUMERIC_INVERSION_TRIAD_FIRST_INVERSION
                        | NUMERIC_INVERSION_TRIAD_SECOND_INVERSION


// These are more than numeric inversions, they specify an added 7th
// AND a numeric inversion, within the same token
NUMERIC_INVERSION_SEVENTH_FIRST_INVERSION : "65"
NUMERIC_INVERSION_SEVENTH_SECOND_INVERSION : "43"
NUMERIC_INVERSION_SEVENTH_THIRD_INVERSION: "42" | "2"

numeric_inversion_seventh : NUMERIC_INVERSION_SEVENTH_FIRST_INVERSION
                          | NUMERIC_INVERSION_SEVENTH_SECOND_INVERSION
                          | NUMERIC_INVERSION_SEVENTH_THIRD_INVERSION

//////////////////
// Added intervals
//////////////////
INTERVAL_UNISON : "1"
INTERVAL_SECOND : "2"
INTERVAL_THIRD : "3"
INTERVAL_FOURTH : "4"
INTERVAL_FIFTH : "5"
INTERVAL_SIXTH : "6"
INTERVAL_SEVENTH : "7"
INTERVAL_OCTAVE : "8"
INTERVAL_NINTH : "9"
INTERVAL_TENTH : "10"
INTERVAL_ELEVENTH : "11"
INTERVAL_TWELFTH : "12"
INTERVAL_THIRTEENTH : "13"
INTERVAL_FOURTEENTH : "14"
INTERVAL_FIFTEENTH : "15"

valid_diatonic_intervals : INTERVAL_SEVENTH
                         | INTERVAL_NINTH
                         | INTERVAL_ELEVENTH
                         | INTERVAL_THIRTEENTH

valid_descriptive_intervals : INTERVAL_UNISON
                            | INTERVAL_SECOND
                            | INTERVAL_THIRD
                            | INTERVAL_FOURTH
                            | INTERVAL_FIFTH
                            | INTERVAL_SIXTH
                            | INTERVAL_SEVENTH
                            | INTERVAL_OCTAVE
                            | INTERVAL_NINTH
                            | INTERVAL_TENTH
                            | INTERVAL_ELEVENTH
                            | INTERVAL_TWELFTH
                            | INTERVAL_THIRTEENTH
                            | INTERVAL_FOURTEENTH
                            | INTERVAL_FIFTEENTH

MAJOR_INTERVAL : "M"
MINOR_INTERVAL : "m"
PERFECT_INTERVAL : "P"
AUGMENTED_INTERVAL : "A"
DIMINISHED_INTERVAL : "D"
DOUBLE_AUGMENTED_INTERVAL : "AA"
DOUBLE_DIMINISHED_INTERVAL : "DD"

interval_quality : MAJOR_INTERVAL
                 | MINOR_INTERVAL
                 | PERFECT_INTERVAL
                 | AUGMENTED_INTERVAL
                 | DIMINISHED_INTERVAL
                 | DOUBLE_AUGMENTED_INTERVAL
                 | DOUBLE_DIMINISHED_INTERVAL

diatonic_added_interval : [interval_quality] valid_diatonic_intervals

// For adding arbitrary intervals, it is mandatory to specify the interval quality
descriptive_added_interval : interval_quality valid_descriptive_intervals

////////////////////
// Missing intervals
////////////////////
MISSING_ROOT : "1"
MISSING_THIRD: "3"
MISSING_FIFTH: "5"

valid_missing_intervals : MISSING_ROOT
                        | MISSING_THIRD
                        | MISSING_FIFTH

missing_interval : "x" valid_missing_intervals

////////////////////
// Triad alterations
////////////////////
DIMINISHED_TRIAD_SYMBOL : "o"
AUGMENTED_TRIAD_SYMBOL : "+"

major_triad : [alteration] scale_degree_major
augmented_triad : major_triad AUGMENTED_TRIAD_SYMBOL
minor_triad : [alteration] scale_degree_minor
diminished_triad : minor_triad DIMINISHED_TRIAD_SYMBOL

/////////
// Triads
/////////
triad : major_triad
      | augmented_triad
      | minor_triad
      | diminished_triad

/////////////////////
// Triad-based chords
/////////////////////
triad_chord : triad [triad_inversion] missing_interval*
            | triad [numeric_inversion_triad] missing_interval*

extended_triad_chord: triad diatonic_added_interval [chord_inversion] missing_interval*
                    | triad numeric_inversion_seventh missing_interval*

/////////////////
// Special chords
/////////////////
SPECIAL_GERMAN : "Ger" | "Gn"
SPECIAL_ITALIAN : "Lt" | "It"
SPECIAL_FRENCH : "Fr"
augmented_sixth : SPECIAL_GERMAN [chord_inversion]
                | SPECIAL_ITALIAN [triad_inversion]
                | SPECIAL_FRENCH [chord_inversion]

SPECIAL_NEAPOLITAN : "N"
SPECIAL_TRISTAN : "Tr"
SPECIAL_HALF_DIMINISHED_SEVENTH : "vii0"
SPECIAL_CADENTIAL_SIX_FOUR : "Cad"
SPECIAL_COMMON_TONE_DIMINISHED : "CTdim7" | "CTo7"

special_chord : augmented_sixth
              | SPECIAL_NEAPOLITAN [triad_inversion]
              | SPECIAL_NEAPOLITAN [numeric_inversion_triad]
              | SPECIAL_TRISTAN [chord_inversion]
              | SPECIAL_HALF_DIMINISHED_SEVENTH [chord_inversion]
              | SPECIAL_HALF_DIMINISHED_SEVENTH [numeric_inversion_seventh]
              | SPECIAL_CADENTIAL_SIX_FOUR
              | SPECIAL_COMMON_TONE_DIMINISHED [chord_inversion]
              | SPECIAL_COMMON_TONE_DIMINISHED [numeric_inversion_seventh]

/////////////////////
// Descriptive chords
/////////////////////
descriptive_chord : "?" triad descriptive_added_interval* [extended_chord_inversion]

/////////
// Chords
/////////
chord : triad_chord | extended_triad_chord | special_chord | descriptive_chord

///////////////
// Harm++ Label
///////////////
harmpp : [key] chord ( "/" scale_degree )*
implicit : "(" harmpp ")"
alternate : harmpp "[" harmpp "]"
          | harmpp ( "|" harmpp )~1..4

start : harmpp | implicit | alternate
