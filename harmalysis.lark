// The harmalysis language for harmonic analysis and roman numerals
//
// Copyright (c) 2019, Nestor Napoles Lopez
// All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//
// 1. Redistributions of source code must retain the above copyright notice, this
//    list of conditions and the following disclaimer.
//
// 2. Redistributions in binary form must reproduce the above copyright notice,
//    this list of conditions and the following disclaimer in the documentation
//    and/or other materials provided with the distribution.
//
// 3. Neither the name of the copyright holder nor the names of its
//    contributors may be used to endorse or promote products derived from
//    this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
// AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
// IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
// FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
// DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
// SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
// CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
// OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.


/////////////////////////////////
// Flats, sharps, and alterations
/////////////////////////////////
FLAT :         "-"  | "b"
DOUBLE_FLAT :  "--" | "bb"
SHARP :        "#"
DOUBLE_SHARP : "##" | "x"
alteration : FLAT         -> flat
           | DOUBLE_FLAT  -> double_flat
           | SHARP        -> sharp
           | DOUBLE_SHARP -> double_sharp

///////////////////////////////////////////
// Lower-case, upper-case letters, and keys
///////////////////////////////////////////
NOTE_LETTER_LOWERCASE : "a".."g"
NOTE_LETTER_UPPERCASE : "A".."G"

_MINOR_NATURAL :  "nat"
_MINOR_HARMONIC : "har"
_MINOR_MELODIC :  "mel"

key : NOTE_LETTER_UPPERCASE                                -> major
    | NOTE_LETTER_UPPERCASE alteration                     -> major_with_alteration
    | NOTE_LETTER_LOWERCASE                                -> default_minor
    | NOTE_LETTER_LOWERCASE alteration                     -> default_minor_with_alteration
    | NOTE_LETTER_LOWERCASE            "_" _MINOR_NATURAL  -> natural_minor
    | NOTE_LETTER_LOWERCASE alteration "_" _MINOR_NATURAL  -> natural_minor_with_alteration
    | NOTE_LETTER_LOWERCASE            "_" _MINOR_HARMONIC -> harmonic_minor
    | NOTE_LETTER_LOWERCASE alteration "_" _MINOR_HARMONIC -> harmonic_minor_with_alteration
    | NOTE_LETTER_LOWERCASE            "_" _MINOR_MELODIC  -> melodic_minor
    | NOTE_LETTER_LOWERCASE alteration "_" _MINOR_MELODIC  -> melodic_minor_with_alteration

reference_key : key ":"

established_key : key "=>:"

?explicit_key : reference_key | established_key

////////////////
// Scale degrees
////////////////
TONIC_UPPERCASE :          "I"
SUPERTONIC_UPPERCASE :     "II"
MEDIANT_UPPERCASE :        "III"
SUBDOMINANT_UPPERCASE :    "IV"
DOMINANT_UPPERCASE :       "V"
SUBMEDIANT_UPPERCASE :     "VI"
SEVENTH_DEGREE_UPPERCASE : "VII"

TONIC_LOWERCASE :          "i"
SUPERTONIC_LOWERCASE :     "ii"
MEDIANT_LOWERCASE :        "iii"
SUBDOMINANT_LOWERCASE :    "iv"
DOMINANT_LOWERCASE :       "v"
SUBMEDIANT_LOWERCASE :     "vi"
SEVENTH_DEGREE_LOWERCASE : "vii"

?scale_degree_major : TONIC_UPPERCASE
                    | SUPERTONIC_UPPERCASE
                    | MEDIANT_UPPERCASE
                    | SUBDOMINANT_UPPERCASE
                    | DOMINANT_UPPERCASE
                    | SUBMEDIANT_UPPERCASE
                    | SEVENTH_DEGREE_UPPERCASE

?scale_degree_minor : TONIC_LOWERCASE
                    | SUPERTONIC_LOWERCASE
                    | MEDIANT_LOWERCASE
                    | SUBDOMINANT_LOWERCASE
                    | DOMINANT_LOWERCASE
                    | SUBMEDIANT_LOWERCASE
                    | SEVENTH_DEGREE_LOWERCASE

/////////////////////
// Fifth of the triad
/////////////////////
AUGMENTED_FIFTH :  "+"
DIMINISHED_FIFTH : "o"

/////////
// Triads
/////////
triad : scale_degree_major                             -> major_triad
      | alteration scale_degree_major                  -> major_triad_with_alteration
      | scale_degree_major            AUGMENTED_FIFTH  -> augmented_triad
      | alteration scale_degree_major AUGMENTED_FIFTH  -> augmented_triad_with_alteration
      | scale_degree_minor                             -> minor_triad
      | alteration scale_degree_minor                  -> minor_triad_with_alteration
      | scale_degree_minor            DIMINISHED_FIFTH -> diminished_triad
      | alteration scale_degree_minor DIMINISHED_FIFTH -> diminished_triad_with_alteration

//////////////////
// Added intervals
//////////////////
INTERVAL_UNISON :     "1"
INTERVAL_SECOND :     "2"
INTERVAL_THIRD :      "3"
INTERVAL_FOURTH :     "4"
INTERVAL_FIFTH :      "5"
INTERVAL_SIXTH :      "6"
INTERVAL_SEVENTH :    "7"
INTERVAL_OCTAVE :     "8"
INTERVAL_NINTH :      "9"
INTERVAL_TENTH :      "10"
INTERVAL_ELEVENTH :   "11"
INTERVAL_TWELFTH :    "12"
INTERVAL_THIRTEENTH : "13"
INTERVAL_FOURTEENTH : "14"
INTERVAL_FIFTEENTH :  "15"

MAJOR_INTERVAL :             "M"
MINOR_INTERVAL :             "m"
PERFECT_INTERVAL :           "P"
AUGMENTED_INTERVAL :         "A"
DIMINISHED_INTERVAL :        "D"
DOUBLE_AUGMENTED_INTERVAL :  "AA"
DOUBLE_DIMINISHED_INTERVAL : "DD"

perfect_interval_quality : PERFECT_INTERVAL           -> perfect
                         | AUGMENTED_INTERVAL         -> augmented
                         | DIMINISHED_INTERVAL        -> diminished
                         | DOUBLE_AUGMENTED_INTERVAL  -> double_augmented
                         | DOUBLE_DIMINISHED_INTERVAL -> double_diminished

nonperfect_interval_quality : MAJOR_INTERVAL             -> major
                            | MINOR_INTERVAL             -> minor
                            | AUGMENTED_INTERVAL         -> augmented
                            | DIMINISHED_INTERVAL        -> diminished
                            | DOUBLE_AUGMENTED_INTERVAL  -> double_augmented
                            | DOUBLE_DIMINISHED_INTERVAL -> double_diminished

any_interval_quality : MAJOR_INTERVAL             -> major
                     | MINOR_INTERVAL             -> minor
                     | PERFECT_INTERVAL           -> perfect
                     | AUGMENTED_INTERVAL         -> augmented
                     | DIMINISHED_INTERVAL        -> diminished
                     | DOUBLE_AUGMENTED_INTERVAL  -> double_diminished
                     | DOUBLE_DIMINISHED_INTERVAL -> double_augmented

added_seventh :                                INTERVAL_SEVENTH    -> added_seventh_diatonic
              |    nonperfect_interval_quality INTERVAL_SEVENTH    -> added_seventh_with_quality
added_ninth   :                                INTERVAL_NINTH      -> added_ninth_diatonic
              |    nonperfect_interval_quality INTERVAL_NINTH      -> added_ninth_with_quality
added_eleventh :                               INTERVAL_ELEVENTH   -> added_eleventh_diatonic
               |   perfect_interval_quality    INTERVAL_ELEVENTH   -> added_eleventh_with_quality
added_thirteenth :                             INTERVAL_THIRTEENTH -> added_thirteenth_diatonic
                 | nonperfect_interval_quality INTERVAL_THIRTEENTH -> added_thirteenth_with_quality

_tertian_interval : added_seventh
                  | added_ninth
                  | added_eleventh
                  | added_thirteenth

descriptive_interval : perfect_interval_quality    INTERVAL_UNISON
                     | nonperfect_interval_quality INTERVAL_SECOND
                     | nonperfect_interval_quality INTERVAL_THIRD
                     | perfect_interval_quality    INTERVAL_FOURTH
                     | perfect_interval_quality    INTERVAL_FIFTH
                     | nonperfect_interval_quality INTERVAL_SIXTH
                     | nonperfect_interval_quality INTERVAL_SEVENTH
                     | perfect_interval_quality    INTERVAL_OCTAVE
                     | nonperfect_interval_quality INTERVAL_NINTH
                     | nonperfect_interval_quality INTERVAL_TENTH
                     | perfect_interval_quality    INTERVAL_ELEVENTH
                     | perfect_interval_quality    INTERVAL_TWELFTH
                     | nonperfect_interval_quality INTERVAL_THIRTEENTH
                     | nonperfect_interval_quality INTERVAL_FOURTEENTH
                     | perfect_interval_quality    INTERVAL_FIFTEENTH

////////////////////////
// Inversions by numbers
////////////////////////
TRIAD_FIRST_INVERSION_NUMERIC :  "6"
TRIAD_SECOND_INVERSION_NUMERIC : "64"

triad_inversion_by_number : TRIAD_FIRST_INVERSION_NUMERIC
                          | TRIAD_SECOND_INVERSION_NUMERIC


// These are more than numeric inversions, they specify an added 7th
// AND a numeric inversion, within the same token
SEVENTHCHORD_FIRST_INVERSION_NUMERIC :  "65"
SEVENTHCHORD_SECOND_INVERSION_NUMERIC : "43"
SEVENTHCHORD_THIRD_INVERSION_NUMERIC:   "42" | "2"

seventhchord_inversion_by_number : SEVENTHCHORD_FIRST_INVERSION_NUMERIC
                                 | SEVENTHCHORD_SECOND_INVERSION_NUMERIC
                                 | SEVENTHCHORD_THIRD_INVERSION_NUMERIC

///////////////////////
// Inversions by letter
///////////////////////
TRIAD_INVERSION_LETTER :           "a".."c"
SEVENTHCHORD_INVERSION_LETTER :    "a".."d"
NINTHCHORD_INVERSION_LETTER :      "a".."e"
ELEVENTHCHORD_INVERSION_LETTER :   "a".."f"
THIRTEENTHCHORD_INVERSION_LETTER : "a".."g"

triad_inversion_by_letter :           TRIAD_INVERSION_LETTER           -> inversion_by_letter
seventhchord_inversion_by_letter :    SEVENTHCHORD_INVERSION_LETTER    -> inversion_by_letter
ninthchord_inversion_by_letter :      NINTHCHORD_INVERSION_LETTER      -> inversion_by_letter
eleventhchord_inversion_by_letter :   ELEVENTHCHORD_INVERSION_LETTER   -> inversion_by_letter
thirteenthchord_inversion_by_letter : THIRTEENTHCHORD_INVERSION_LETTER -> inversion_by_letter

////////////////////
// Missing intervals
////////////////////
MISSING_ROOT :     "1"
MISSING_THIRD :    "3"
MISSING_FIFTH :    "5"
MISSING_SEVENTH :  "7"
MISSING_NINTH :    "9"
MISSING_ELEVENTH : "11"

_MISSING_INTERVAL_SYMBOL : "x"

_missing_intervals_triad :           [_MISSING_INTERVAL_SYMBOL MISSING_ROOT] [_MISSING_INTERVAL_SYMBOL MISSING_THIRD] [_MISSING_INTERVAL_SYMBOL MISSING_FIFTH]
_missing_intervals_seventhchord :    [_missing_intervals_triad]
_missing_intervals_ninthchord :      [_missing_intervals_seventhchord]  [_MISSING_INTERVAL_SYMBOL MISSING_SEVENTH]
_missing_intervals_eleventhchord :   [_missing_intervals_ninthchord]    [_MISSING_INTERVAL_SYMBOL MISSING_NINTH]
_missing_intervals_thirteenthchord : [_missing_intervals_eleventhchord] [_MISSING_INTERVAL_SYMBOL MISSING_ELEVENTH]

missing_intervals_triad :           _missing_intervals_triad
missing_intervals_seventhchord :    _missing_intervals_seventhchord
missing_intervals_ninthchord :      _missing_intervals_ninthchord
missing_intervals_eleventhchord :   _missing_intervals_eleventhchord
missing_intervals_thirteenthchord : _missing_intervals_thirteenthchord

/////////////////
// Tertian chords
/////////////////
tertian_chord : triad                                                      missing_intervals_triad
              | triad triad_inversion_by_number                            missing_intervals_triad
              | triad seventhchord_inversion_by_number                     missing_intervals_seventhchord
              | triad triad_inversion_by_letter                            missing_intervals_triad
              | triad added_seventh                                        missing_intervals_seventhchord
              | triad added_seventh    seventhchord_inversion_by_letter    missing_intervals_seventhchord
              | triad added_ninth                                          missing_intervals_ninthchord
              | triad added_ninth      ninthchord_inversion_by_letter      missing_intervals_ninthchord
              | triad added_eleventh                                       missing_intervals_eleventhchord
              | triad added_eleventh   eleventhchord_inversion_by_letter   missing_intervals_eleventhchord
              | triad added_thirteenth                                     missing_intervals_thirteenthchord
              | triad added_thirteenth thirteenthchord_inversion_by_letter missing_intervals_thirteenthchord

/////////////////
// Special chords
/////////////////
GERMAN :                  "Ger" | "Gn"
ITALIAN :                 "Lt"  | "It"
FRENCH :                  "Fr"
NEAPOLITAN :              "N"
TRISTAN :                 "Tr"
HALF_DIMINISHED_SEVENTH : "vii0"
CADENTIAL_SIX_FOUR :      "Cad"
COMMON_TONE_DIMINISHED :  "CTo7"

german :                  GERMAN                  -> name
italian :                 ITALIAN                 -> name
french :                  FRENCH                  -> name
neapolitan :              NEAPOLITAN              -> name
tristan :                 TRISTAN                 -> name
half_diminished_seventh : HALF_DIMINISHED_SEVENTH -> name
cadential_six_four :      CADENTIAL_SIX_FOUR      -> name
common_tone_diminished :  COMMON_TONE_DIMINISHED  -> name

special_chord : german
              | german seventhchord_inversion_by_letter
              | german seventhchord_inversion_by_number
              | italian
              | italian triad_inversion_by_letter
              | italian triad_inversion_by_number
              | french
              | french seventhchord_inversion_by_letter
              | french seventhchord_inversion_by_number
              | neapolitan
              | neapolitan triad_inversion_by_letter
              | neapolitan triad_inversion_by_number
              | tristan
              | half_diminished_seventh ["7"]
              | half_diminished_seventh ["7"] seventhchord_inversion_by_letter
              | half_diminished_seventh seventhchord_inversion_by_number
              | cadential_six_four ["64"]
              | common_tone_diminished
              | common_tone_diminished seventhchord_inversion_by_letter
              | common_tone_diminished seventhchord_inversion_by_number

// /////////////////////
// // Descriptive chords
// /////////////////////
// descriptive_chord : "?" triad added_interval_descriptive* [extended_chord_inversion_letter]

// /////////
// // Chords
// /////////
// chord : tertian_chord | special_chord | descriptive_chord

start : explicit_key tertian_chord
      | explicit_key special_chord


// ///////////////////
// // Harmalysis Label
// ///////////////////
// tonicization : "/" scale_degree
// harmalysis : [key] chord tonicization*
// implicit : "(" harmalysis ")"
// alternate : harmalysis "[" harmalysis "]"
//           | harmalysis ( "|" harmalysis )~1..3

// start : harmalysis | implicit | alternate